// A resolvable configuration to collect test reports data
configurations.create('testReportData') {
    canBeResolved = true
    canBeConsumed = false
    attributes {
        attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.DOCUMENTATION))
        attribute(DocsType.DOCS_TYPE_ATTRIBUTE, objects.named(DocsType, 'test-report-data'))
    }
}

configurations.create('integrationTestReportData') {
    canBeResolved = true
    canBeConsumed = false
    attributes {
        attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.DOCUMENTATION))
        attribute(DocsType.DOCS_TYPE_ATTRIBUTE, objects.named(DocsType, 'integration-test-report-data'))
    }
}

tasks.register('testReport', Copy) {
    from configurations.testReportData
    into layout.buildDirectory.dir('test-results/tests').get().asFile
    include '*.xml'
}

tasks.register('integrationTestReport', Copy) {
    from configurations.integrationTestReportData
    into layout.buildDirectory.dir('test-results/integrationTests').get().asFile
    include '*.xml'
}
